archetype (adl_version=2.0.6; rm_release=0.0.1)
	CIMI-CORE-WoundAssertion.wound_assertion.v1.0.0

specialize
	CIMI-CORE-FindingSiteAssertion.finding_site_assertion.v1

language
  original_language = <[ISO_639-1::en]>

description
  original_author = <
    ["name"] = <"Claude Nanjo">
    ["organisation"] = <"Cognitive Medical Systems">
    ["email"] = <"cnanjo@gmail.com">
    ["date"] = <"06/15/2016">
  >
  details = <
    ["en"] = <
      language = <[ISO_639-1::en]>
      purpose = <"CIMI wound assertion archetype">
      use = <" - ">
      keywords = <"assertion","wound">
    >
  >
  other_details = <["model_level"] = <"reference">>
  lifecycle_state = <"unmanaged">
  other_contributors = <"Joey Coyle <joey@xcoyle.com>","Linda Bird <lbi@ihtsdo.org>","Stan Huff <Stan.Huff@imail.org>","Harold Solbrig <Solbrig.Harold@mayo.edu>","Thomas Beale <thomas.beale@oceaninformatics.com>">
  custodian_namespace = <"org.opencimi">
  custodian_organisation = <"Clinical Information Modeling Initiative <http://opencimi.org>">
  licence = <"Creative Commons CC-BY <https://creativecommons.org/licenses/by/3.0/>">
  copyright = <"Copyright (c) Clinical Information Modelling Initiative <http://opencimi.org>">
  ip_acknowledgements = <
    ["loinc"] = <"This content from LOINC® is copyright © 1995 Regenstrief Institute, Inc. and the LOINC Committee, and available at no cost under the license at http://loinc.org/terms-of-use">
    ["snomed"] = <"This specification includes SNOMED Clinical Terms® (SNOMED CT®) which is used by permission of the International Health Terminology Standards Development Organization (IHTSDO). All rights reserved. SNOMED CT® was originally created by the College of American Pathologists. 'SNOMED' and 'SNOMED CT' are registered trademarks of the IHTSDO (www.ihtsdo.org). Use of any SNOMED CT content (including codes, identifiers, terms, expressions or derivatives) in any operational system (including any Data Creation System or Data Analysis System as defined in the SNOMED CT Affiliate License Agreement) is subject to the terms and conditions of the SNOMED CT Affiliate License (www.ihtsdo.org/license.pdf) or to a written variation to that agreement that has been agreed and formally executed between the IHTSDO and the Affiliate Licensee.">
  >

definition
	WoundAssertion[id1.1.1.1.1.1] matches {
		/result/code matches {[ac0.2.1.0.1.1]}
		dueTo matches {
			Concept[id0.0.0.5.1]
		}
		periwoundCondition matches {
			Concept[id0.0.0.0.1] matches {code matches {[ac0.0.0.0.1]}}
		}
		woundSize matches {
			DimensionalFeature[id0.0.0.0.50] matches {
				area matches {
					QUANTITY[id0.0.0.0.51]
				}
				circumference matches {
					QUANTITY[id0.0.0.0.52]
				}
				volume matches {
					QUANTITY[id0.0.0.0.53]
				}
				width matches {
					QUANTITY[id0.0.0.0.54]
				}
				depth matches {
					QUANTITY[id0.0.0.0.55]
				}
				length matches {
					QUANTITY[id0.0.0.0.56]
				}
			}
		}
		tunneling matches {
			Tunneling[id0.0.0.0.100] matches {
				direction matches {
					Concept[id0.0.0.0.101] matches {code matches {[ac0.0.0.0.2]}}
				}
				length matches {
					QUANTITY[id0.0.0.0.102]
				}
			}
		}
		undermining matches {
			Undermining[id0.0.0.0.200] matches {
				direction matches {
					Concept[id0.0.0.0.201] matches {code matches {[ac0.0.0.0.3]}}
				}
				length matches {
					QUANTITY[id0.0.0.0.202]
				}
			}
		}
		woundBedAndEdge matches {
			WoundBedAndEdge[id0.0.0.0.300] matches {
				woundBaseAppearance matches {
					Concept[id0.0.0.0.301] matches {code matches {[ac0.0.0.0.4]}}
				}
				woundBedAppearanceToWoundBaseBedRatio matches {
					PROPORTION[id0.0.0.0.302]
				}
				woundBaseColor matches {
					Concept[id0.0.0.0.303] matches {code matches {[ac0.0.0.0.5]}}
				}
				woundEdgeAppearance matches {
					Concept[id0.0.0.0.304] matches {code matches {[ac0.0.0.0.6]}}
				}
				woundEdgeColor matches {
					Concept[id0.0.0.0.305] matches {code matches {[ac0.0.0.0.7]}}
				}
				woundClosureType matches {
					Concept[id0.0.0.0.306] matches {code matches {[ac0.0.0.0.8]}}
				}
			}
		}
		exudate matches {
			WoundExudate[id0.0.0.0.400] matches {
				odor matches {
					Concept[id0.0.0.0.401] matches {code matches {[ac0.0.0.0.9]}}
				}
				color matches {
					Concept[id0.0.0.0.402] matches {code matches {[ac0.0.0.0.10]}}
				}
				drainageAmount matches {
					QUANTITY[id0.0.0.0.403]
				}
				appearance matches {
					Concept[id0.0.0.0.404] matches {code matches {[ac0.0.0.0.11]}}
				}
			}
		}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1.1.1.1.1.1"] = <
				text = <"Wound Assertion">
				description = <"Experimental class based on a more elaborate specialization of the Assertion class - the wound assertion pattern.">
			>
			["ac0.0.0.0.1"] = <
				text = <"Periwound condition reference set (foundation metadata concept)">
				description = <"The periwound conditition describes the skin around the wound. Color, induration, warmth and edema should be assessed. Redness of the surrounding skin can be indicative of unrelieved prssure. Irritation of the surrounding skin can result from exposure to feces or urine, a reaction to the dressing or tape, or inappropriate removal of dressing or tape. Redness, tenderness, warmth and swelling are classical clinical signs of infection.">
			>
			["id0.0.0.5.1"] = <
				text = <"Mechanism of injury">
				description = <"The mechanism of injury that led to the wound.">
			>
			["id0.0.0.0.1"] = <
				text = <"Description of periwound">
				description = <"A description of the skin around the wound (periwound). (LOINC)">
			>
			["id0.0.0.0.50"] = <
				text = <"Wound size">
				description = <"Features of the wound such as length, width, depth, area, circumference, etc...">
			>
			["id0.0.0.0.51"] = <
				text = <"Wound area">
				description = <"The two-dimensional extent of the wound.">
			>
			["id0.0.0.0.52"] = <
				text = <"Wound circumference">
				description = <"The distance around the outside edge of the wound">
			>
			["id0.0.0.0.53"] = <
				text = <"Wound volume">
				description = <"The volume of missing tissue the volume of missing and morbid tissue">
			>
			["id0.0.0.0.54"] = <
				text = <"Wound width">
				description = <"The distance across the surface of the wound perpendicular to the wound length">
			>
			["id0.0.0.0.55"] = <
				text = <"Wound length">
				description = <"The longest distance across the surface of the wound">
			>
			["id0.0.0.0.56"] = <
				text = <"Wound depth">
				description = <"The longest distance from the superficial plane to the bottom of the wound">
			>
			["id0.0.0.0.100"] = <
				text = <"Tunneling">
				description = <"Whether tunneling is present.">
			>
			["id0.0.0.0.101"] = <
				text = <"Tunneling direction">
				description = <"Direction from center of wound to tunneling, expressed as the clock number if the 12 position were at the cranial, superior, or proximal position of the wound">
			>
			["ac0.0.0.0.2"] = <
				text = <"Valueset - Tunneling Direction">
				description = <"-">
			>
			["id0.0.0.0.102"] = <
				text = <"Tunneling length">
				description = <"Distance from the wound edge to the farthest extent of the tunneling.">
			>
			["id0.0.0.0.200"] = <
				text = <"Undermining">
				description = <"Whether undermining is present.">
			>
			["ac0.0.0.0.3"] = <
				text = <"Valueset - Undermining Direction">
				description = <"-">
			>
			["id0.0.0.0.201"] = <
				text = <"Undermining direction">
				description = <"Direction from center of wound to undermining, expressed as the clock number if the 12 position were at the cranial, superior, or proximal position of the wound.">
			>
			["id0.0.0.0.202"] = <
				text = <"Undermining length">
				description = <"Distance from the wound edge to the farthest extent of the undermining.">
			>
			["id0.0.0.0.300"] = <
				text = <"Wound bed and edge">
				description = <"Wound bed and edge.">
			>
			["id0.0.0.0.301"] = <
				text = <"Wound base appearance">
				description = <"Physiological state of wound surface tissue as evaluated by visual inspection">
			>
			["ac0.0.0.0.4"] = <
				text = <"Wound bed appearance reference set (foundation metadata concept)">
				description = <"This refset provides values for recording and retrieving clinical observations/findings/phenomena obtained in examination of a wound bed or wound base.  Wound bed appearance depends upon the stage of healing; if healing well, may not be differentiated from the edge">
			>
			["id0.0.0.0.302"] = <
				text = <"Wound bed appearance to wound base bed ratio">
				description = <"the area of identified wound bed appearance (necrotic, sloughing, etc.; see LOINC 72371-8) compared to the total area of the wound bed. For example, 40% of the wound bed is necrotic. Results can either be subjective or measured.">
			>
			["id0.0.0.0.303"] = <
				text = <"Wound base color">
				description = <"Color of the base of the wound.">
			>
			["ac0.0.0.0.5"] = <
				text = <"Wound tissue color reference set (foundation metadata concept)">
				description = <"The purpose of this refset is to document the colors within the base of the wound. The mobile app 'Tissue Analytics' color and texture to determine color. They store 'red' to represent granulation, 'yellow' to represent slough and 'black' to represent necrosis. VA nurses want to have additional colors such as 'purple' which may indicate infection by a specific organism.">
			>
			["id0.0.0.0.304"] = <
				text = <"Wound edge appearance">
				description = <"Edge appearance.">
			>
			["ac0.0.0.0.6"] = <
				text = <"Wound edge description reference set (foundation metadata concept)">
				description = <"This refset contains values for for recording and retrieving descriptions of a wound edge.">
			>
			["id0.0.0.0.305"] = <
				text = <"Wound edge color">
				description = <"Color of the edge of the wound.">
			>
			["ac0.0.0.0.7"] = <
				text = <"Valueset - Wound Base and Edge Wound Edge Color">
				description = <"-">
			>
			["id0.0.0.0.306"] = <
				text = <"Wound closure type">
				description = <"Wound closure type.">
			>
			["ac0.0.0.0.8"] = <
				text = <"Valueset - Wound Base and Edge Wound Closure Type">
				description = <"-">
			>
			["id0.0.0.0.400"] = <
				text = <"Exudate">
				description = <"Wound exudate.">
			>
			["id0.0.0.0.401"] = <
				text = <"Exudate color">
				description = <"The color of the exudate.">
			>
			["ac0.0.0.0.10"] = <
				text = <"Wound exudate color reference set (foundation metadata concept)">
				description = <"The purpose of this RefSet is to describe the color of exudate that is present in a wound.  The values will be used in conjunction with the values from the Wound exudate amount ordinal Refset and the Wound exudate appearance refset to document and retrieve values that describe wound exudate.">
			>			
			["id0.0.0.0.402"] = <
				text = <"Exudate odor">
				description = <"The odor of the exudate.">
			>
			["ac0.0.0.0.9"] = <
				text = <"Exudate odor reference set (foundation metadata concept)">
				description = <"This refset is to record and retrieve data related to exudate odor.  ">
			>
			["id0.0.0.0.403"] = <
				text = <"Exudate drainage amount">
				description = <"The quantification of the exudate coming out of the wound.">
			>
			["id0.0.0.0.404"] = <
				text = <"Exudate appearance">
				description = <"The appearance of the exudate coming out of the wound.">
			>
			["ac0.0.0.0.11"] = <
				text = <"Wound exudate appearance reference set (foundation metadata concept)">
				description = <"The purpose of this RefSet is to describe the consistency of exudate that is present in a wound.  The values will be used for recording and retrieving the consistency of exudate.">
			>
			["ac0.2.1.0.1.1"] = <
				text = <"Wound Assertion Code Range">
				description = <"Wound assertion code range.">
			>
		>
	>

	term_bindings = <
		["loinc"] = <
      		items = <
		        ["id1.1.1.1.1"] = <http://loinc.org/id/39135-9> --  Wound assessment panel
		        ["id0.0.0.0.1"] = <http://loinc.org/id/72301-5> --  Description of Periwound
		        ["id0.0.0.0.100"] = <http://loinc.org/id/72298-3> --  Tunneling of wound
		        ["id0.0.0.0.101"] = <http://loinc.org/id/72297-5> --  Tunneling direction
		        ["id0.0.0.0.102"] = <http://loinc.org/id/72296-7> --  Tunneling length
		        ["id0.0.0.0.200"] = <http://loinc.org/id/72295-9> --  Undermining of wound
		        ["id0.0.0.0.201"] = <http://loinc.org/id/72294-2 > --  Undermining direction
		        ["id0.0.0.0.202"] = <http://loinc.org/id/72293-4> --  Undermining length
		        ["id0.0.0.0.300"] = <http://loinc.org/id/72372-6> -- Wound bed and edge
		        ["id0.0.0.0.301"] = <http://loinc.org/id/72371-8> -- Appearance of Wound base
		        ["id0.0.0.0.302"] = <http://loinc.org/id/72370-0> -- Area of identified wound bed appearance/​Area of wound bed of Wound base
		        ["id0.0.0.0.303"] = <http://loinc.org/id/39132-6> -- Color of Wound base
		        ["id0.0.0.0.304"] = <http://loinc.org/id/72304-9> -- Edge of wound description
		        ["id0.0.0.0.305"] = <http://loinc.org/id/39133-4> -- Color of Wound edge
		        ["id0.0.0.0.306"] = <http://loinc.org/id/72305-6> -- Wound closure type
		        ["id0.0.0.0.400"] = <http://loinc.org/id/72292-6 > --  Wound exudate
		        ["id0.0.0.0.401"] = <http://loinc.org/id/39116-9> --  Drainage amount of Wound
		        ["id0.0.0.0.402"] = <http://loinc.org/id/39115-1 > --  Drainage description of Wound Narrative
		        ["id0.0.0.0.403"] = <http://loinc.org/id/72288-4 > --   Odor of Exudate from wound 
		        ["id0.0.0.0.404"] = <http://loinc.org/id/72289-2 > --   Color of Exudate from wound 
      		>
    	>
    	["snomed-ct"] = <
		    items = <
		        ["ac0.0.0.0.1"] = <http://snomed.info/id/91000205108> -- Periwound condition refset
		        ["ac0.0.0.0.2"] = <http://example.com/valueset/tunneling_direction_range_vs.v0.0.1> 
		        ["ac0.0.0.0.3"] = <http://example.com/valueset/undermining_direction_range_vs.v0.0.1>
		        ["ac0.0.0.0.4"] = <http://snomed.info/id/121000205104> -- Wound bed appearance refset
		        ["ac0.0.0.0.5"] = <http://snomed.info/id/151000205105> -- Wound tissue color refset
		        ["ac0.0.0.0.6"] = <http://snomed.info/id/161000205108> -- Wound edge description refset
		        ["ac0.0.0.0.7"] = <http://example.com/valueset/wound_bed_and_edge_wound_edge_color_range_vs.v0.0.1>
		        ["ac0.0.0.0.8"] = <http://example.com/valueset/wound_bed_and_edge_wound_closure_type_range_vs.v0.0.1>
		        ["ac0.0.0.0.9"] = <http://snomed.info/id/341000205109> -- Exudate odor reference set
		        ["ac0.0.0.0.10"] = <http://snomed.info/id/191000205101> -- Wound exudate color refset
		        ["ac0.0.0.0.11"] = <http://snomed.info/id/181000205103> -- Wound exudate appearance refset
		    >
		>
    	
  	>
